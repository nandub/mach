AC_INIT(README)

AS_VERSION(mach, MACH, 0, 4, 4, 1)
AC_SUBST(RELEASE, $MACH_RELEASE)

AC_SUBST_FILE(AUTHORS)
AUTHORS=$srcdir/AUTHORS

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AC_SUBST(ACLOCAL_AMFLAGS, "-I m4")

AS_AC_EXPAND(SYSCONFDIR, $sysconfdir)
AC_MSG_NOTICE(Storing configuration files in $SYSCONFDIR)

AS_AC_EXPAND(LOCALSTATEDIR, $localstatedir)
AC_MSG_NOTICE(Using localstatedir $LOCALSTATEDIR)

dnl rootsdir
AC_ARG_ENABLE(rootsdir,
  AC_HELP_STRING([--enable-rootsdir=PATH], [create roots in PATH]),
  [ rootsdir=$enableval ],
  [ rootsdir=$localstatedir/lib/mach/roots]
)
AS_AC_EXPAND(ROOTSDIR, $rootsdir)
AC_MSG_NOTICE(Using rootsdir $ROOTSDIR)

dnl archivesdir
AC_ARG_ENABLE(archivesdir,
  AC_HELP_STRING([--enable-archivesdir=PATH], [store archives in PATH]),
  [ archivesdir=$enableval ],
  [ archivesdir=$localstatedir/cache/mach/archives]
)
AS_AC_EXPAND(ARCHIVESDIR, $archivesdir)
AC_MSG_NOTICE(Using archivesdir $ARCHIVESDIR)

dnl statesdir
AC_ARG_ENABLE(statesdir,
  AC_HELP_STRING([--enable-statesdir=PATH], [use states in PATH]),
  [ statesdir=$enableval ],
  [ statesdir=$localstatedir/lib/mach/states]
)
AS_AC_EXPAND(STATESDIR, $statesdir)
AC_MSG_NOTICE(Using statesdir $STATESDIR)

dnl builduser
AC_ARG_ENABLE(builduser,
  AC_HELP_STRING([--enable-builduser=USERNAME], [use USERNAME for builds]),
  [ BUILDUSER=$enableval ],
  [ BUILDUSER=machbuild ]
)
AC_SUBST(BUILDUSER)
AC_MSG_NOTICE(Using build username $BUILDUSER)

dnl buildgroup
AC_ARG_ENABLE(buildgroup,
  AC_HELP_STRING([--enable-buildgroup=GROUPNAME], [use GROUPNAME for builds]),
  [ BUILDGROUP=$enableval ],
  [ BUILDGROUP=machbuild ]
)
AC_SUBST(BUILDGROUP)
AC_MSG_NOTICE(Using build groupname $BUILDGROUP)

dnl default root
dnl first try to guess the platform
CHECK_RELEASE(fedora,    "(Yarrow)",   defaultroot="fedora-1-i386")

CHECK_RELEASE(redhat,    "(Guinness)", defaultroot="redhat-70-i386")
CHECK_RELEASE(redhat,    "(Seawolf)",  defaultroot="redhat-71-i386")
CHECK_RELEASE(redhat,    "(Enigma)",   defaultroot="redhat-72-i386")
CHECK_RELEASE(redhat,    "(Valhalla)", defaultroot="redhat-73-i386")
CHECK_RELEASE(redhat,    "(Psyche)",   defaultroot="redhat-80-i386")
CHECK_RELEASE(redhat,    "(Shrike)",   defaultroot="redhat-9-i386")

CHECK_RELEASE(yellowdog, "(Sirius)",   defaultroot="yellowdog-30-ppc")

dnl now append a modifier if asked for

AC_ARG_WITH(flavor,
  AC_HELP_STRING([--with-flavor=FLAVOR], [set flavor of root to FLAVOR]),
  [ flavor="${withval}" ],
  [ flavor=]
)
if test "x$flavor" != "x"
then
  defaultroot="$defaultroot-$flavor"
fi

AC_ARG_WITH(defaultroot,
  AC_HELP_STRING([--with-defaultroot=ROOT], [set default root to ROOT]),
  [ defaultroot=${withval} ]
)
AS_AC_EXPAND(DEFAULTROOT, $defaultroot)
AC_MSG_NOTICE(Using default root $DEFAULTROOT)

AS_AC_EXPAND(SBINDIR, $sbindir)
AC_MSG_NOTICE(Placing mach-helper in $SBINDIR)

dnl check for C compiler
AC_PROG_CC

dnl check for python
AS_PATH_PYTHON(2.2)
if test "x$PYTHON" == "x"; then
  AC_MSG_ERROR(No suitable python version found)
fi

dnl output stuff
AC_OUTPUT(
Makefile
etc/Makefile
etc/conf
etc/dist.d/Makefile
m4/Makefile
mach.spec
scripts/Makefile
scripts/mach
src/Makefile
)
